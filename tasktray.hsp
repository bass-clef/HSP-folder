	
	#module __TASKTRAY__
	#uselib "kernel32.dll"
	#func  GetModuleFileName	"GetModuleFileNameA"	int, int, int

	#uselib "shell32.DLL"
	#func  ExtractIconEx		"ExtractIconExA"		str, int, int, int, int
	#func  Shell_NotifyIcon		"Shell_NotifyIcon"		int, int

	#define	NIM_ADD			0x00000000
	#define	NIM_DELETE		0x00000002
	#define	NIM_MODIFY		0x00000001
	#define	NIF_ICON		0x00000002
	#define	NIF_MESSAGE		0x00000001
	#define	NIF_TIP			0x00000004
	#define	global	NOTIFY_TRAYICON	0x00001400
	
	// タスクトレイにアイコンを追加
	#deffunc TTIAdd str text
		// 自分自身のファイル名を取得
		sdim fName, PATH_MAX
		GetModuleFileName NULL, varptr(fName), PATH_MAX
		hSIcon = NULL
		ExtractIconEx fName, 0, NULL, varptr(hSIcon), 1

		// NOTIFYICONDATA 構造体
		dim IcoData, 22
		IcoData.0 = 88, hwnd@hsp, 1, NIF_MESSAGE | NIF_ICON | NIF_TIP
		IcoData.4 = NOTIFY_TRAYICON, hSIcon
		poke IcoData, 24, text

		// タスクトレイにアイコンを追加
		Shell_NotifyIcon NIM_ADD, varptr(IcoData)
	return hIcon
	
	// タスクトレイからアイコンを削除
	#deffunc TTIDel
		dim IcoData, 22
		IcoData.0 = 88, hwnd@hsp, 1, NIF_ICON
		Shell_NotifyIcon NIM_DELETE, varptr(IcoData)
	return
	
	// タスクトレイのアイコンを変更
	#deffunc TTIChangeIcon int hIcon
		if hIcon == NULL :return -1
		dim IcoData, 22
		IcoData.0 = 88, hwnd@hsp, 1, NIF_ICON, 0, hIcon
		
		// タスクトレイアイコンを変更
		Shell_NotifyIcon NIM_MODIFY, varptr(IcoData)
	return 0
	
	// タスクトレイのメッセージを変更
	#deffunc TTIChangeText str text
		if hIcon == NULL :return -1
		dim IcoData, 22
		IcoData.0 = 88, hwnd@hsp, 1, NIF_TIP
		poke IcoData, 24, text
		
		// タスクトレイのチップヘルプを変更
		Shell_NotifyIcon NIM_MODIFY, varptr(IcoData)
	return
	
	#global

